#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars);
use Getopt::Long;
use Pod::Usage;

use FusionInventory::Agent;
use FusionInventory::Agent::Worker;

Getopt::Long::Configure('no_ignorecase');

my %options;
GetOptions(
    \%options,
    'help',
    'version',
    'debug',
    'lazy',
    'wait=i',
    'task=s',
    'task-param=s@',
    'target=s',
    'target-param=s@',
    'conf-file=s'
) or pod2usage(-verbose => 0);

if ($options{help}) {
    pod2usage(-verbose => 0);
}
if ($options{version}) {
    print $FusionInventory::Agent::VERSION_STRING . "\n";
    exit 0;
}
if (!$options{task}) {
    pod2usage(
        -message => 'No task given, aborting',
        -verbose => 0,
        -exitval => 2,
    );
}
if (!$options{target}) {
    pod2usage(
        -message => 'No target given, aborting',
        -verbose => 0,
        -exitval => 2,
    );
}

my ($task_params, $target_params);
foreach my $param (@{$options{'task-param'}}) {
    die "invalid task parameter $param"
        unless $param =~ /^(\S+)=(\S+)$/;
    $task_params->{$1} = $2;
}
foreach my $param (@{$options{'target-param'}}) {
    die "invalid target parameter $param"
        unless $param =~ /^(\S+)=(\S+)$/;
    $target_params->{$1} = $2;
}

if ($options{wait}) {
    my $delay = int rand($options{wait});
    sleep($delay);
}

my $worker = FusionInventory::Agent::Worker->new(
    confdir   => './etc/fusioninventory',
    datadir   => './share',
    vardir    => './var',
    debug     => $options{debug},
    lazy      => $options{lazy},
    conffile  => $options{'conf-file'},
);

eval {
    $worker->run(
        task          => $options{task},
        target        => $options{target},
        task_params   => $task_params,
        target_params => $target_params
    );
};

if ($EVAL_ERROR) {
    print STDERR $EVAL_ERROR;
    exit 2;
}
__END__

=head1 NAME

fusioninventory-worker - FusionInventory worker

=head1 SYNOPSIS

B<fusioninventory-worker> [options] --target <target> --task <task>

  Options:
    --conf-file FILE         configuration file
    --debug                  debug mode
    --lazy                   do not run out of schedule for this target
    --wait TIME              wait a random delay before running
    --task-param key=value   pass additional parameter to the task
    --target-param key=value pass additional parameter to the task
    --version                display the agent version
    --help                   display usage instructions

=head1 EXAMPLES

    % fusioninventory-worker --target localhost --task inventory
    # execute task 'inventory' for target 'localhost'

=head1 DESCRIPTION

F<fusioninventory-worker> executes a single task for a single target.
