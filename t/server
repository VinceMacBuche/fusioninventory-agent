#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use English '-no_match_vars';
use File::Temp;
use HTTP::Daemon;
use HTTP::Status;

my %options = (
    port => 8080
);
GetOptions(
    \%options,
    'port=i',
    'pidfile=s',
    'verbose'
);

my $daemon = HTTP::Daemon->new(
    LocalAddr => 'localhost',
    LocalPort => $options{port},
    Reuse     => 1
) or die "can't create daemon: $ERRNO";

if (my $pid = fork()) {
    print "test server running with pid $pid\n";

    if ($options{pidfile}) {
        open (my $handle, '>', $options{pidfile})
            or die "Can't open file $options{pidfile}";
        print $handle "$pid\n";
        close $handle;
    }

    exit 0;
} else {
    while (my $client = $daemon->accept()) {
        while (my $request = $client->get_request()) {
            if ($request->method() eq 'GET') {
                my $path = $request->uri()->path();
                $client->send_response("received $path");
                print "received $path\n";
            } else {
                $client->send_error(RC_FORBIDDEN)
            }
        }
        $client->close();
    }

    if ($options{pidfile}) {
        unlink $options{pidfile};
    }
}
